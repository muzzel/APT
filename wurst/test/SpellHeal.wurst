package SpellHeal
import StatHandler
import ChannelSpellPreset
import AbilityObjEditing

public constant int SPELL_RAW_HEAL = 'A000'

public function combatTextHeal(unit source, unit target, real heal)
	createTTEx(target.getPos3fake(), vec2(GetRandomReal(-0.02, 0.02), 0.04), R2I(heal).toString(), 8., 1.2, colorA(0, 255, 0, 255), GetOwningPlayer(source))
	..setPermanent(false)
	..setFadepoint(0.6)

function spellHeal(unit caster, unit target)
	let heal = 10 + 2. * caster.getStat(Stat.INT)
	target.setHP(target.getHP() + heal)
	AddSpecialEffectTarget("Abilities\\Spells\\Human\\HolyBolt\\HolyBoltSpecialArt.mdl", target, "overhead").destr()
	combatTextHeal(caster, target, heal)

init
	let onCast = CreateTrigger()
	for n = 0 to 11
		onCast.registerPlayerUnitEvent(Player(n), EVENT_PLAYER_UNIT_SPELL_EFFECT, null)
	onCast.addAction(() -> begin
		if GetSpellAbilityId() == SPELL_RAW_HEAL
			spellHeal(GetTriggerUnit(), GetSpellTargetUnit())
	end)

@compiletime function genObjects()
	new ChannelSpellPreset(SPELL_RAW_HEAL, 1)
		..removeChannelProperties()
		..setOption(Option.VISIBLE, true)
		..setTargetType(Targettype.UTARGET)
		..setName("Heal")
		..setTooltipNormal(1, "Heal")
		..setHotkeyNormal("Q")
		..setTooltipNormalExtended(1, "Heals the target by |cffffcc0010 + 2 x Int(caster)|r.")
		..setManaCost(1, 8)
		..setCooldown(1, 10)
		..setIconNormal("ReplaceableTextures\\CommandButtons\\BTNStaffOfSilence.blp")
		..setEffect("")
		..setCaster("")
		..setTarget("")
		..setButtonPositionResearchX(5)